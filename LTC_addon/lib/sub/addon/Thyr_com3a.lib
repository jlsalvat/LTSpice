
*************************************************************************************************************
*                                 commande THYRISTOR   COMMANDE PONT BAS POUR  PONT  TRIPHASE
************************************************************************************************************
* commande thyristor triphasé 50 Hz

.SUBCKT Thyr_com3N P1 P2 P3 com1 n1 com2 n2 com3 N3 ton1={ton} alpha1={alpha}

***** bloc commande thyristor PD3 50Hz alpha et ton en paramètres

*entrée P1,P3 sortie comTh1
*commande Thyristor V(P1)>0
**** bloc retard alpha par rapport à 0V entree p1 sortie N001
R1 P1 0 1G
XU6 P1 P3 BB1 opamp VCC=10 Aol=100K GBW=10Meg
E1 N001  0 LAPLACE {V(bb1)} {exp(-s*(5.556e-5*alpha1))}

*monostable de durée ton entree N001 sortie m1
A1 N002 0 0 0 0 N003 0 0 BUF
A2 0 N003 0 N001 0 0 m1 0 AND
R2 N001 0 1G
E2 N002 0 LAPLACE {V(N001)} {exp(-s*ton1)}
***** pulse ton  1V f=10kHz  entree m1,clk sortie out1
V2 clk 0 PULSE(0 10 0 0 0 50u 100u)
A3 0 m1 0 clk 0 0 out1 0 AND
***** bloc transfo entree out1 sortie out3
E3 out2 n1 out1 0 10
R5 com1 out2 1k

*entrée P2,P1 sortie comTh2
*commande Thyristor 
**** bloc retard alpha par rapport à 0V entree p1 sortie N0011
R11 P2 0 1G
XU7 P2 P1 BB11 opamp VCC=10 Aol=100K GBW=10Meg
E11 N0011  0 LAPLACE {V(bb11)} {exp(-s*(5.556e-5*alpha1))}

*monostable de durée ton entree N0011 sortie m11
A11 N0021 0 0 0 0 N0031 0 0 BUF
A21 0 N0031 0 N0011 0 0 m11 0 AND
R21 N0011 0 1G
E21 N0021 0 LAPLACE {V(N0011)} {exp(-s*ton1)}
***** pulse ton  1V f=10kHz  entree m1,clk sortie out1
A31 0 m11 0 clk 0 0 out11 0 AND
***** bloc transfo entree out1 sortie out3
E31 out21 n2 out11 0 10
R51 com2 out21 1k

*entrée P3,P2 sortie comTh3
*commande Thyristor 
**** bloc retard alpha par rapport à 0V entree p1 sortie N0011
R12 P3 0 1G
XU8 P3 P2 BB12 opamp VCC=10 Aol=100K GBW=10Meg
E12 N0012  0 LAPLACE {V(bb12)} {exp(-s*(5.556e-5*alpha1))}

*monostable de durée ton entree N0011 sortie m11
A12 N0022 0 0 0 0 N0032 0 0 BUF
A22 0 N0032 0 N0012 0 0 m12 0 AND
R22 N0012 0 1G
E22 N0022 0 LAPLACE {V(N0012)} {exp(-s*ton1)}
***** pulse ton  1V f=10kHz  entree m1,clk sortie out1
A32 0 m12 0 clk 0 0 out12 0 AND
***** bloc transfo entree out1 sortie out3
E32 out22 n3 out12 0 10
R52 com3 out22 1k
.lib opamp.sub
.ENDS    Thyr_com3
