* INA170 SPICE Macro-model 
*          
*   Rev. A    27 June 2003, by W.K. Sands
*
*   Rev. B    4 August 2003 By Neil Albaugh: ADDED HEADER TEXT & EDITED TEXT
*
*	This macromodel has been optimized to model the AC, DC, and transient response performance within 
*     the device data sheet specified limits. 
*     Correct operation of this macromodel has been verified on MicroSim P-Spice ver. 8.0 and on 
*     PENZAR Development TopSPICE ver. 6.82d. For help with other analog simulation software, 
*     please consult your software supplier. 
*
*  
*   Copyright 2003 by Texas Instruments Corporation
*
*
* OPERATING NOTES
*
* MODEL TEMPERATURE RANGE IS -55 C TO +150 C, NOT ALL PARAMETERS ACCURATELY TRACK 
* THOSE OF AN ACTUAL INA170 OVER THE FULL TEMPERATURE RANGE BUT ARE AS CLOSE AS PRACTICAL
*
* BEGIN MODEL INA170
*
* PINOUT ORDER VIN- VIN+ VREF GND ROS OUT V+
* PINOUT ORDER   1    2    3   4   5   6   8
*
* OPERATING NOTES
* THE INTERNAL OFFSET SOURCE, V71 IS SET
* AT 1 MILLIVOLT SO THE OUTPUT STAGE IS
* ENOUGH ON WITH ZERO SIGNAL TO GIVE THE
* PROPER BANDWIDTH IN AN AC ANALYSIS WITH
* NO INPUT SIGNAL OR OFFSET PRESENT. THIS
* WILL GIVE SLIGHTLY LARGER THAN TYPICAL
* OUTPUT ERRORS. FOR TYPICAL OUTPUT ERRORS
* CHANGE V71 TO 0.35 MILLIVOLT.
*
* MODEL FEATURES
* THIS MODEL FEATURES INPUT RANGE, OUTPUT
* SWING, OUTPUT COMPLIANCE, BANDWIDTH,
* GAIN VS RL, CMRR VS FREQ, PSRR VS FREQ,
* SETTLING TIME, QUIESCENT CURRENT AND
* QUIESCENT CURRENT VERSUS SUPPLY VOLTAGE,
* OUTPUT CURRENT DRAWN THROUGH THE SUPPLY,
* OUTPUT IMPEDANCE, INPUT OFFSET, INPUT
* BIAS CURRENT, AND NOISE. THE OFFSETING
* AMPLIFIER HAS LIMITED COMMON MODE
* VOLTAGE RANGE AND LIMITED BANDWIDTH.
*
.SUBCKT INA170 1 2 3 4 5 6 8
V71 14 12 1E-3
Q1 9 10 11 QPI
Q2 10 10 11 QPI
Q3 10 10 12 QPI
Q4 13 10 12 QPI
R1 11 2 1E3
R2 14 1 1E3
Q5 15 15 8 QPI
Q6 16 15 8 QPI
Q7 9 17 18 QI
Q8 8 9 17 QI
Q9 16 19 18 QI
Q10 8 13 19 QI
Q11 13 20 18 QI
Q12 11 16 21 QI
Q13 21 21 16 QI
I1 17 18 5E-6
I2 10 18 5E-6
I3 19 18 5E-6
R3 21 6 500
I4 15 18 20E-6
R5 22 16 8E3
C1 13 22 1.5E-12
C6 18 6 5E-12
C7 6 8 0.5E-12
E2 23 17 8 0 0.15E-6
E3 20 23 2 0 -2.5E-6
D1 18 11 DD
D2 18 14 DD
D3 11 24 DD
D4 14 24 DD
D5 8 24 DD
D6 18 8 DD
D7 6 24 DD
D8 18 6 DD
R10 18 24 1E9
R11 23 20 1E9
R12 17 23 1E9
L1 18 4 1N
R13 18 4 10
C8 18 2 1E-12
C9 18 1 1E-12
Q14 25 26 27 QPI
Q15 28 5 29 QI
Q16 14 25 5 QI
Q17 5 5 25 QI
I5 8 30 6E-6
R15 31 25 20E3
C10 26 31 8E-12
Q18 26 3 32 QI
Q19 33 30 18 QI
Q20 30 30 18 QI
Q21 28 28 34 QPI
Q22 26 28 35 QPI
R16 34 8 1E3
R17 35 8 1E3
R18 36 29 10E3
R19 36 32 10E3
R20 27 8 1E3
Q23 25 37 18 QI
Q24 37 37 18 QI
I6 8 37 3E-6
V74 36 33 0.25
D9 5 24 DD
D10 18 5 DD
D11 18 3 DD
D12 3 24 DD
I17 0 38 1E-3
D15 38 0 DD
V76 39 38 -0.658
R22 0 39 10E3
G2 8 18 39 0 -65E-6
I18 18 8 15E-6
.MODEL DD D IS=3.0E-14 BV=90 RS=10
.MODEL QI NPN
+IS=1E-15 BF=200 BR=0.1 NF=1 NR=1 EG=1.2056 ISE=1E-17
+NE=1.5 ISC=1E-17 NC=1.5 VAF=125 VAR=15 IKF=6E-3 RB=1.2E3
+RBM=65 RE=5 IKR=6E-3 IRB=60E-6 RC=750 CJE=0.5E-12 VJE=0.77
+MJE=0.3 CJC=0.6E-12 VJC=0.64 MJC=0.425 CJS=0.05E-12 VJS=0.58
+MJS=0.5  FC=0 XCJC=1 TF=300E-12 TR=1E-6 XTF=1
+VTF=0 ITF=10E-3 PTF=5 XTI=2.3
+ XTB=1.5 KF=0.16E-15 AF=1
.MODEL QPI PNP
+IS=1E-15 BF=200 BR=0.1 NF=1 NR=1 EG=1.2056 ISE=1E-17
+NE=1.5 ISC=1E-17 NC=1.5 VAF=125 VAR=15 IKF=6E-3 RB=1.2E3
+RBM=65 RE=5 IKR=6E-3 IRB=60E-6 RC=750 CJE=0.5E-12 VJE=0.77
+MJE=0.3 CJC=0.6E-12 VJC=0.64 MJC=0.425 CJS=0.05E-12 VJS=0.58
+MJS=0.5  FC=0 XCJC=1 TF=50E-9 TR=1E-6 XTF=1
+VTF=0 ITF=10E-3 PTF=5 XTI=2.3
+ XTB=1.5 KF=0.16E-15 AF=1
.ENDS
* END MODEL INA170
