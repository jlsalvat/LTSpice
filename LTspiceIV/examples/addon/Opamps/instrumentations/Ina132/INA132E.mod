*

*

*  ------------------------------------------------------------------------ 

* |  NOTICE: THE INFORMATION PROVIDED HEREIN IS BELIEVED TO BE RELIABLE;   |

* |  HOWEVER; BURR-BROWN ASSUMES NO RESPONSIBILITY FOR INACCURACIES OR     |

* |  OMISSIONS.  BURR-BROWN ASSUMES NO RESPONSIBILITY FOR THE USE OF THIS  |

* |  INFORMATION, AND ALL USE OF SUCH INFORMATION SHALL BE ENTIRELY AT     |

* |  THE USER'S OWN RISK.  NO PATENT RIGHTS OR LICENSES TO ANY OF THE      |

* |  CIRCUITS DESCRIBED HEREIN ARE IMPLIED OR GRANTED TO ANY THIRD PARTY.  |

* |  BURR-BROWN DOES NOT AUTHORIZE OR WARRANT ANY BURR-BROWN PRODUCT FOR   |

* |  USE IN LIFE-SUPPORT DEVICES AND/OR SYSTEMS.                           |

*  ------------------------------------------------------------------------ 

*

*

*

* INA132E = A3_132E OPERATIONAL AMPLIFIER + PRECISION RESISTOR NETWORK

* "E" IS ENHANCED MODEL

* CREATED ON 11/11/96 AT 2:50

* REV.A

* REV.B  20 JUNE 97 NPA: MOVED LEGAL DISCLAIMER TO BEGINNING OF FILE

*

***** INA132E SUB-CIRCUIT

* CONNECTIONS:          NON-INVERTING INPUT

*                       |  INVERTING INPUT

*                       |  |  POSITIVE POWER SUPPLY

*                       |  |  |  NEGATIVE POWER SUPPLY

*                       |  |  |  |  OUTPUT

*                       |  |  |  |  |  REFERENCE

*                       |  |  |  |  |  |  SENSE

*                       |  |  |  |  |  |  |

.SUBCKT INA132E         1  2  3  4  5  8  9

*

***** A3_132E SUB-CIRCUIT

* CONNECTIONS:          NON-INVERTING INPUT

*                       |  INVERTING INPUT

*                       |  |  POSITIVE POWER SUPPLY

*                       |  |  |  NEGATIVE POWER SUPPLY

*                       |  |  |  |  OUTPUT

*                       |  |  |  |  |

X1                     14 13  3  4  5  A3_132E

*

  R1   2  13   40000

  R2   13  9   39997

  C2   13  9   .15p

  R3   1  14   40000

  R4   14  8   40000

*

*

.ENDS

*

* INA132E operational amplifier "macromodel" subcircuit

* created using Parts release 6.0 on 11/08/96 at 17:04

* Parts is a MicroSim product.

* Notes on construction of the INA132E Extended Macromodel

*

* Differential and Common-Mode capacitances for the op-amp have been

*  eliminated because the inputs to the op-amp are not brought out of

*  the package. This limits the capacitance at those nodes. Any effect

*  on the AC performance of the part (particularly the AC CMRR) is

*  minimal because of the INA132's bandwidth of 300kHz

*

* The noise of the simulated op-amp is larger than that in the actual

*  circuit. This comes from the op-amp that was generated by the Micro-

*  Sim Parts program. It should be noted that the dominant source of

*  voltage noise is the four 40K resistors. VN1, VN2, DN1, DN2, and EN

*  are included so that the 1/f noise is accurately simulated.

*  The input bias current is also roughly half of the actual circuit.

*  This discrepancy also comes from the Parts program.

*

* Simulations involving capacitive load drive are approximate. In the

*  actual circuit, the output stage characteristics change more

*  with the type of load. Swing to the rails are also modeled with 

*  approximations for the same reasons.

*

* While using the simulation in single supply, the simulation may not

*  give the correct result for the current coming out of the supplies

*  due to the nature of the model. The model was designed for dual

*  supply operation.

*

* For further information see the INA132 data sheet and the Burr-Brown

*  Spice Based Macromodels Applications Bulletin.

*

*

* connections:   non-inverting input

*                | inverting input

*                | | positive power supply

*                | | | negative power supply

*                | | | | output

*                | | | | |

.subckt A3_132E  1 2 3 4 5

*

  c1   11 12 7.279E-12

  c2    6  7 40.00E-12

  dc    5 53 dx

  de   54  5 dx

  dlp  90 91 dx

  dln  92 90 dx

  dp    4  3 dx

  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5

  fb    7 99 poly(5) vb vc ve vlp vln 0 1.326E9 -1E9 1E9 1E9 -1E9

  ga    6  0 11 12 75.40E-6

  gcm   0  6 10 99 377.0E-12

  iee   3 10 dc 4.730E-6

  hlim 90  0 vlim 1K

  q1   11  2 13 qx

  q2   12 64 14 qx          

  r2    6  9 100.0E3

  rc1   4 11 13.26E3

  rc2   4 12 13.26E3

  re1  13 10 .9k           

  re2  14 10 .9k       

  ree  10 99 42.28E6

  ro1   8  5  200           

  ro2   7 99  200           

  vb    9  0 dc 0

  vc    3 53 dc 1.350

  ve   54  4 dc .80

  vlim  7  8 dc 0

  vlp  91  0 dc 12

  vln   0 92 dc 12





**********************************

*                                *

* Additions for the              *

* INA132E (enhanced) macro model *

*                                *

**********************************



  rp    3  4  5E6           ; replace rp w/ rp and ip to

                            ; get correct Iq change w/supply

  dq1  20  21  dx

  dq2  22  20  dx

  vq1  21  0   0

  vq2  22  0   0

  fq1  3   0   poly(1) vq1  149u  1      ; Models current from

  fq2  0   4   poly(1) vq2  149u  -1     ; supplies with load

  fq3  0   20  poly(1) vlim 0     1



  vn1  61  0   .6                        ; Noise model (1/f portion)

  vn2  0   62  .6

  dn1  61  63  dy

  dn2  63  62  dy

  en   64  1   63  0  .6



.model dx D(Is=800.0E-18)

.model dy D(Is=1.9E-16 AF=1 KF=2.258E-17)

.model qx PNP(Is=800.0E-18 Bf=300)

.ENDS 

*.ENDS INA132E

*

*



