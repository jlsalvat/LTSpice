********************************************************************************
*
*  A simplified and multi-functional approach to mathematical functions. Simply
* place Math1(r) symbol in the schematic and then choose the desired math
* function through the drop-down menu in the symbol's properties. Or simply
* right-click on it's name and rename it, for example from '+' to 'x'.
*  The reason for this is usability, easier workflow and, not lastly, a modular
* approach, meaning many other functions can be added to the list without the
* need to create a symbol for each.
*
*									Vlad
*
********************************************************************************
*
* [GENERAL]
*
* - Math2(r) means symbol with two variables. The following functions are
* available:
*
*  function		|  list name
* --------------+------------
*	 a+b		|	+
*	 a-b		|	-
*	 a*b		|	*
*	 a/b		|	/
*	a==b		|	~~
*	a<>b		|	<>
*	a<=b		|	<=
*	a<b			|	<
* atan2(a,b)	|	atan2
* hypot(a,b)	|	hypot
*   max(a,b)	|	max
*   min(a,b)	|	min
*   mod(a,b)	|	mod
*
*
* [PARAMETERS]
*
* a,b		= control the signum of the inputs for atan2();
* dc	[V]	= "DC" adjustment for atan2();
* min	[V]	= minimum estimate value to avoid division by zero (a/b).
*
* - Hidden:
*
* tripdv,tripdt	= LTspice's specific for B-sources, default 0/0 (LTspice handles
*		them).
*

*
* [CHARACTERISTICS]
*
* - All the symbols have floating input and Rout = 1 Ohm.
*
*
* [NOTES]
*
* - There is also a symbol named Math2r, which is added for compatibility with
* the previous basic algebra functions, for easier replacing. It does the same
* things as Math2.
*
************************
*
*	a+b
*
************************
.subckt + a b out
G1 0 out a 0 1
G2 0 out b 0 1
R1 0 out 1
.ends +
************************
*
*	a-b
*
************************
.subckt - a b out
G1 0 out a b 1
R1 out 0 1
.ends -
************************
*
*	a*b
*
************************
*
* [NOTES]
*
* - The ISO8859-15 character "×" could have been used for a better looking name,
* but it's much easier to simply right-click and rename it to "x" than going
* through all the drop-down menu choice.
*
************************
.subckt x a b out
B1 0 out I=v(a)*v(b) Rpar=1 tripdv={tripdv} tripdt={tripdt}
.ends x
********************************
*
*	x*y (a*b v2.0)
*
********************************
*
* [NOTES]
*
* - In circuits where the v(b) doesn't violate LTspice's instantaneous feeback
* condition, this is be faster than the previous version, provided tripdv/tripdt
* settings aren't tinkered with. At any rate, it's provided as another choice.
*
************************
.subckt x2 a b out
G1 0 out a 0 1
R1 out 0 R=v(b)
;R2 out 0 R={if(v(b)==0,1u,v(b))} tripdv={tripdv} tripdt={tripdt}
.ends x2
************************
*
*	a/b
*
************************
*
* [NOTES]
*
* - The ISO8859-15 character "÷" could have been used for a better looking name,
* but it's much easier to simply right-click and rename "/" than going through
* all the drop-down menu choice.
*
************************
.subckt / a b out
.param min=0
B1 0 out I=v(a)/if( abs(v(b))<min,1,v(b) ) Rpar=1 tripdv={tripdv} tripdt={tripdt}
.ends /
************************
*
*	a==b et a<>b
*	modif salvat
*   ajout d'une fenêtre de comparaison, sinon ca ne fonctionne pas
*   ne fonctionne que sous certaines conditions de simulation (fonction du pas de calcul...)
*   on préfèrera utiliser les comparateurs inf ou inf_eq
************************
*
* [NOTES]
*
* - Names with "=" won't be recognized by LTspice and using " '==' " will make
* the name visible in the list but not usable. Thus, its name is "~~".
*
************************
*
* [PARAMETRES]
*
* utiliser les paramètres si nécessaire... ( A TESTER en fonction des signaux d'entrées et du pas de simultation)
*.param tripdv=1u
*.param tripdt=1u
.param Delta=1m

************************
*
*	a==b
*
************************
.subckt ~~ a b out
B1 0 out Rpar=1 I=( (v(a)>=(v(b)-{delta})) & (v(a)<=(v(b)+{delta})) ) tripdv={tripdv} tripdt={tripdt}
.ends ~~
************************
*
*	a<>b
*
************************
.subckt <> a b out
B1 0 out Rpar=1 I=inv( (v(a)>=(v(b)-{delta})) & (v(a)<=(v(b)+{delta})) ) tripdv={tripdv} tripdt={tripdt}
.ends <>
************************
*
*	a<=b
*
************************
.subckt inf_eq a b out
B1 0 out Rpar=1 I=( v(a)<=v(b) ) tripdv={tripdv} tripdt={tripdt}
.ends inf_eq
************************
*
*	a<b
*
************************
.subckt inf a b out
B1 0 out Rpar=1 I=( v(a)<v(b) ) tripdv={tripdv} tripdt={tripdt}
.ends inf_eq
************************
*
*	atan2(a,b)
*
************************
.subckt atan2 a b out
.param dc=0  a=1  b=1
B1 0 out I=atan2(sgn(a)*v(a),sgn(b)*v(b))+dc rpar=1 tripdv={tripdv} tripdt={tripdt}
.ends atan2
************************
*
*	hypot(a,b)
*
************************
.subckt hypot a b out
B1 0 out I=hypot(v(a),v(b)) rpar=1 tripdv={tripdv} tripdt={tripdt}
.ends hypot
************************
*
*	max(a,b)
*
************************
.subckt max a b out
B1 0 out I=max(v(a),v(b)) rpar=1 tripdv={tripdv} tripdt={tripdt}
.ends max
************************
*
*	min(a,b)
*
************************
.subckt min a b out
B1 0 out I=min(v(a),v(b)) rpar=1 tripdv={tripdv} tripdt={tripdt}
.ends min
************************
*
*	mod(a,b)
*
************************
.subckt mod a b out
B1 0 out I=v(a)-floor(v(a)/v(b))/v(b) rpar=1 tripdv={tripdv} tripdt={tripdt}
.ends mod
********************************************************************************
*
* [UPDATES]
*
* 2013.02.03	- Minor modifications and corrections to x2.
*
********************************************************************************
